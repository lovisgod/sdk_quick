apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion targetSdk
    defaultConfig {
        applicationId "com.interswitchng.interswitchpos"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        multiDexEnabled true
        versionCode 4
        versionName "1.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        android.applicationVariants.all{
            variant ->
                variant.outputs.all{
                    def date = new Date()
                    def formattedDate = date.format('yyMMdd')
                    String appName = "finca"
                    outputFileName = "${appName}-${variant.versionName}-${formattedDate}.apk"
                }
        }
    }

    signingConfigs {
        paxConfig {
            keyAlias 'PAX'
            keyPassword 'B6H2A7Y8WG'
            storeFile file(rootDir.absolutePath + '/PAXKeystore.jks')
            storePassword 'B6H2A7Y8WG'
        }
    }


    buildTypes {
        release {
            minifyEnabled minified
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.paxConfig
            applicationIdSuffix '.v2.release'
            buildConfigField("boolean", "MOCK", "false") // mock device
        }
        debug {
            buildConfigField("boolean", "MOCK", "false") // mock device
            applicationIdSuffix '.v2.debug'
        }
    }

    flavorDimensions "terminal"
    productFlavors{
        telpo {
            dimension "terminal"
            applicationIdSuffix ".telpo"
            versionNameSuffix "-telpo"
        }
        pax {
            dimension "terminal"
            applicationIdSuffix ".pax"
            versionNameSuffix "-pax"
        }
    }
    compileOptions {
        targetCompatibility javaVersion
        sourceCompatibility javaVersion
    }

    configurations {
        telpoReleaseImplementation
        telpoDebugImplementation

        paxReleaseImplementation
        paxDebugImplementation

        releaseImplementation
        debugImplementation
    }


    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation "androidx.multidex:multidex:$multidex_version"

    //releaseImplementation "com.interswitchng:smart-pos-core:$versionName"
    //paxDebugImplementation "com.interswitchng:smart-pos-emv-pax:$versionName"
    //releaseImplementation "com.interswitchng:smart-pos-usb:$versionName"


    // kotlin coroutine libs
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    debugImplementation project(':smart-pos-core')
    debugImplementation project(':smart-pos-usb')
    paxDebugImplementation project(':smart-pos-emv-pax')
    telpoDebugImplementation project(':smart-pos-emv-telpo')


    releaseImplementation project(':smart-pos-core')
    releaseImplementation project(':smart-pos-usb')
    paxReleaseImplementation project(':smart-pos-emv-pax')
    telpoReleaseImplementation project(':smart-pos-emv-telpo')


    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "org.koin:koin-test:$koin_version"


    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}
repositories {
    mavenCentral()
}
